# Init CMake (require at least version 2.6)
CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

# General build settings
IF (NOT CMAKE_BUILD_TYPE)
	SET(CMAKE_BUILD_TYPE "Release" CACHE STRING "Possible build types: None Debug Release RelWithDebInfo MinSizeRel")
ENDIF()

IF (NOT ENABLE_VISUALIZER)
	SET(ENABLE_VISUALIZER 0 CACHE BOOL "Enable compiling and linking the Visualizer module")
ENDIF()

IF (NOT ENABLE_IMAGEGENERATOR)
	SET(ENABLE_IMAGEGENERATOR 0 CACHE BOOL "Enable image generator")
ENDIF()

IF (NOT ENABLE_IMAGESTREAMERVISUALIZER)
	SET (ENABLE_IMAGESTREAMERVISUALIZER 0 CACHE BOOL "Enable compiling and linking the Image Streamer Visualizer module, this module generates images accumulating a fixed number of spikes")
ENDIF()

IF (NOT ENABLE_NET_STREAM)
	SET(ENABLE_NET_STREAM 0 CACHE BOOL "Enable TCP server module for sending data over the network")
ENDIF()

IF (NOT ENABLE_CAFFEINTERFACE)
	SET(ENABLE_CAFFEINTERFACE 0 CACHE BOOL "Enable Caffe deep learning framework interface module")

	IF (NOT CPU_ONLY)
		SET(CPU_ONLY 0 CACHE BOOL "Set Caffe framework to work only with the CPU, not with GPU support")
	ENDIF()
ENDIF()

IF (NOT DVS128 AND NOT DAVISFX2 AND NOT DAVISFX3)
	MESSAGE(SEND_ERROR "Please specify one of the following options to select a supported device: DVS128, DAVISFX2, DAVISFX3.")
	RETURN()
ENDIF()

# Project name and version
PROJECT(cAER C CXX)
SET(PROJECT_VERSION_MAJOR 0)
SET(PROJECT_VERSION_MINOR 9)
SET(PROJECT_VERSION_PATCH 10)
SET(PROJECT_VERSION_NOREV ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH})
IF (NOT PROJECT_REVISION)
	EXECUTE_PROCESS(COMMAND svnversion OUTPUT_VARIABLE PROJECT_REVISION OUTPUT_STRIP_TRAILING_WHITESPACE)
	# EXECUTE_PROCESS(COMMAND git rev-parse HEAD OUTPUT_VARIABLE PROJECT_REVISION OUTPUT_STRIP_TRAILING_WHITESPACE)
ENDIF()
SET(PROJECT_VERSION ${PROJECT_VERSION_NOREV}-r${PROJECT_REVISION})
MESSAGE(STATUS "Project version is: ${PROJECT_VERSION}")

# Define installation paths.
INCLUDE(GNUInstallDirs)

# Set compiler info
SET(CC_CLANG FALSE)
SET(CC_GCC FALSE)
SET(CC_ICC FALSE)
SET(CC_MSVC FALSE)

IF ("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang")
	SET(CC_CLANG TRUE)
ELSEIF ("${CMAKE_C_COMPILER_ID}" STREQUAL "GNU")
	SET(CC_GCC TRUE)
ELSEIF ("${CMAKE_C_COMPILER_ID}" STREQUAL "Intel")
	SET(CC_ICC TRUE)
ELSEIF ("${CMAKE_C_COMPILER_ID}" STREQUAL "MSVC")
	SET(CC_MSVC TRUE)
ENDIF()

# Test if we are on a big-endian architecture
INCLUDE(TestBigEndian)
TEST_BIG_ENDIAN(SYSTEM_BIGENDIAN)

# C11 standard needed (atomics, threads)
IF (CC_GCC OR CC_CLANG)
        IF(NOT APPLE)
	    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -std=c11") 
        ELSE()
	    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS}")
        ENDIF()
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -stdlib=libstdc++")
ENDIF()

# Check size of various types
INCLUDE(CheckTypeSize)
CHECK_TYPE_SIZE("size_t" SIZEOF_SIZE_T)
CHECK_TYPE_SIZE("void *" SIZEOF_VOID_PTR)

IF (NOT "${SIZEOF_VOID_PTR}" STREQUAL "${SIZEOF_SIZE_T}")
	MESSAGE(SEND_ERROR "Size of void * and size_t must be the same!")
ENDIF()

# Check threads support (almost nobody implements C11 threads yet!)
FIND_PACKAGE(Threads)
SET(HAVE_PTHREADS ${CMAKE_USE_PTHREADS_INIT})
SET(HAVE_WIN32_THREADS ${CMAKE_USE_WIN32_THREADS_INIT})

IF (HAVE_PTHREADS)
	ADD_DEFINITIONS(-DHAVE_PTHREADS=1)

	# POSIX system (Unix, Linux, MacOS X)
	ADD_DEFINITIONS(-D_XOPEN_SOURCE=700)
	ADD_DEFINITIONS(-D_DEFAULT_SOURCE=1)

	IF (APPLE)
		ADD_DEFINITIONS(-D_DARWIN_C_SOURCE=1)
	ENDIF()

	# Support for large files (>2GB) on 32-bit systems
	ADD_DEFINITIONS(-D_FILE_OFFSET_BITS=64)
ENDIF()

IF (HAVE_WIN32_THREADS)
	ADD_DEFINITIONS(-DHAVE_WIN32_THREADS=1)
ENDIF()

IF (ENABLE_IMAGEGENERATOR)
	ADD_DEFINITIONS(-DENABLE_IMAGEGENERATOR=1)
ENDIF()

IF (ENABLE_IMAGESTREAMERVISUALIZER)
	ADD_DEFINITIONS(-DENABLE_IMAGESTREAMERVISUALIZER=1)
ENDIF()

# Forward settings as defines
IF (ENABLE_VISUALIZER)
	ADD_DEFINITIONS(-DENABLE_VISUALIZER=1)
ENDIF()

IF (ENABLE_CAFFEINTERFACE)
	ADD_DEFINITIONS(-DENABLE_CAFFEINTERFACE=1)
	IF (CPU_ONLY)
		ADD_DEFINITIONS(-DCPU_ONLY=1)
	ENDIF()
ENDIF()

IF (ENABLE_NET_STREAM)
	ADD_DEFINITIONS(-DENABLE_NET_STREAM=1)
ENDIF()

IF (DVS128)
	ADD_DEFINITIONS(-DDVS128=1)
ENDIF()

IF (DAVISFX2)
	ADD_DEFINITIONS(-DDAVISFX2=1)
ENDIF()

IF (DAVISFX3)
	ADD_DEFINITIONS(-DDAVISFX3=1)
ENDIF()

# Enable all warnings for GCC / Clang
IF (CC_GCC OR CC_CLANG)
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pedantic -Wall -Wextra")

	IF (CC_GCC)
		# Enable all useful warnings manually in GCC.
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wunused -Wundef -Wformat=2 -Winit-self -Wuninitialized")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wpointer-arith -Wcast-qual -Wcast-align -Wwrite-strings")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wstrict-prototypes -Wmissing-prototypes -Wredundant-decls")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wmissing-declarations -Wnested-externs -Wstack-protector")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wshadow -Wbad-function-cast -Wfloat-equal")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wconversion -Wstrict-overflow=2")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wjump-misses-init -Wunsuffixed-float-constants -Wdouble-promotion")
	ENDIF()

	IF (CC_CLANG)
		# Enable all warnings in Clang easily.
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Weverything -Wno-packed -Wno-padded -Wno-unreachable-code-break")
		SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-disabled-macro-expansion")
	ENDIF()
ENDIF()

# Use same flags for C++ support
SET(CMAKE_CXX_FLAGS ${CMAKE_C_FLAGS})

# Search for external libraries with pkg-config
INCLUDE(FindPkgConfig)

# Basic USB device support and configuration
PKG_CHECK_MODULES(LIBCAER REQUIRED libcaer>=1.0.2)
PKG_CHECK_MODULES(MXML REQUIRED mxml>=2.7)
SET(CAER_LIBS ${LIBCAER_LIBRARIES} ${MXML_LIBRARIES})
SET(CAER_LIBDIRS ${LIBCAER_LIBRARY_DIRS} ${MXML_LIBRARY_DIRS})
SET(CAER_INCDIRS ${LIBCAER_INCLUDE_DIRS} ${MXML_INCLUDE_DIRS})
file(GLOB_RECURSE CAER_INI_FILES modules/ini/*.c)
SET(CAER_SRC_FILES ${CAER_INI_FILES})

# By default, no C++ support is enabled.
SET(CAER_CXX_LIBS "")
SET(CAER_CXX_SRC_FILES "")

# Basic IN/OUT support
file(GLOB_RECURSE CAER_MISC_FILES modules/misc/*.c)
SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_MISC_FILES})

# Common filters
file(GLOB_RECURSE CAER_BA_FILES modules/backgroundactivityfilter/*.c)
SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_BA_FILES})

file(GLOB_RECURSE CAER_STAT_FILES modules/statistics/*.c)
SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_STAT_FILES})

IF (ENABLE_VISUALIZER)
	# Visualizer support via Allegro library
	PKG_CHECK_MODULES(ALLEGRO5 allegro-5)
	PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5)
	PKG_CHECK_MODULES(ALLEGRO5_PRIMITIVES allegro_primitives-5)
	PKG_CHECK_MODULES(ALLEGRO5_FONT allegro_font-5)
	PKG_CHECK_MODULES(ALLEGRO5_TTF allegro_ttf-5)

	IF (NOT ALLEGRO5_FOUND)
		# Check alternative pkg-config naming
		PKG_CHECK_MODULES(ALLEGRO5 allegro-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_MAIN allegro_main-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_PRIMITIVES allegro_primitives-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_FONT allegro_font-5.0)
		PKG_CHECK_MODULES(ALLEGRO5_TTF allegro_ttf-5.0)
	ENDIF()

	IF (NOT ALLEGRO5_FOUND OR NOT ALLEGRO5_MAIN_FOUND)
		MESSAGE(SEND_ERROR "Allegro-5 main library not found!")
		RETURN()
	ENDIF()

	IF (NOT ALLEGRO5_PRIMITIVES_FOUND OR NOT ALLEGRO5_FONT_FOUND OR NOT ALLEGRO5_TTF_FOUND)
		MESSAGE(SEND_ERROR "Allegro-5 primitives, font and ttf addons not found!")
		RETURN()
	ENDIF()

	SET(CAER_LIBS ${CAER_LIBS} ${ALLEGRO5_LIBRARIES} ${ALLEGRO5_MAIN_LIBRARIES})
	SET(CAER_LIBDIRS ${CAER_LIBDIRS} ${ALLEGRO5_LIBRARY_DIRS} ${ALLEGRO5_MAIN_LIBRARY_DIRS})
	SET(CAER_INCDIRS ${CAER_INCDIRS} ${ALLEGRO5_INCLUDE_DIRS} ${ALLEGRO5_MAIN_INCLUDE_DIRS})

	SET(CAER_LIBS ${CAER_LIBS} ${ALLEGRO5_PRIMITIVES_LIBRARIES} ${ALLEGRO5_FONT_LIBRARIES} ${ALLEGRO5_TTF_LIBRARIES})
	SET(CAER_LIBDIRS ${CAER_LIBDIRS} ${ALLEGRO5_PRIMITIVES_LIBRARY_DIRS} ${ALLEGRO5_FONT_LIBRARY_DIRS} ${ALLEGRO5_TTF_LIBRARY_DIRS})
	SET(CAER_INCDIRS ${CAER_INCDIRS} ${ALLEGRO5_PRIMITIVES_INCLUDE_DIRS} ${ALLEGRO5_FONT_INCLUDE_DIRS} ${ALLEGRO5_TTF_INCLUDE_DIRS})

	file(GLOB_RECURSE CAER_VISUALIZER_FILES modules/visualizer_allegro/*.c)
	SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_VISUALIZER_FILES})
ENDIF()

IF (ENABLE_IMAGEGENERATOR)
	file(GLOB_RECURSE CAER_IMAGEGENERATOR_FILES modules/imagegenerator/*.c)
	SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_IMAGEGENERATOR_FILES})
ENDIF()

IF (ENABLE_IMAGESTREAMERVISUALIZER)
	# It requires the Visualizer to be enabled.
	file(GLOB_RECURSE CAER_IMAGESTREAMERVISUALIZER_FILES modules/imagestreamervisualizer/*.c)
	SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_IMAGESTREAMERVISUALIZER_FILES})
ENDIF()

IF (ENABLE_CAFFEINTERFACE)
	# Deep learning: Caffe interface with required libs.
	FIND_PACKAGE(GLOG QUIET)
	IF(Glog_FOUND)
		SET(CAER_INCDIRS ${CAER_INCDIRS} ${Glog_INCLUDE_DIRS})
		MESSAGE(STATUS "Glog FOUND")
		SET(EXTRA_CAFFE_LIBS ${Glog_LIBRARIES})
	ENDIF()

	FIND_PACKAGE(Boost)
	IF(Boost_FOUND)
		SET(CAER_INCDIRS ${CAER_INCDIRS} ${BOOST_INCLUDE_DIRS})
		MESSAGE(STATUS "BOOST FOUND")
		SET(EXTRA_CAFFE_LIBS ${BOOST_LIBRARIES})
	ENDIF(Boost_FOUND)

	FIND_PACKAGE(OpenCV)
	IF(OpenCV_FOUND)
		SET(CAER_INCDIRS ${CAER_INCDIRS} ${OpenCV_INCLUDE_DIRS})
		MESSAGE(STATUS "OPENCV FOUND")
		SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${OpenCV_LIBRARIES})
	ENDIF(OpenCV_FOUND)

	FIND_PACKAGE(Caffe)
	IF(NOT CAFFE_FOUND)
		IF(Caffe_FOUND)
			SET(CAER_INCDIRS ${CAER_INCDIRS} ${Caffe_INCLUDE_DIRS})
			MESSAGE(STATUS ${Caffe_LIBRARIES})
			SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${Caffe_LIBRARIES})
		ENDIF(Caffe_FOUND)
	ELSE()
		IF(CAFFE_FOUND)
			SET(CAER_INCDIRS ${CAER_INCDIRS} ${Caffe_INCLUDE_DIRS})
			MESSAGE(STATUS ${Caffe_LIBRARIES})
			SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${Caffe_LIBRARIES})
		ENDIF(CAFFE_FOUND)
	ENDIF()

	FIND_PACKAGE(Protobuf)
	IF(Protobuf_FOUND)
		SET(CAER_INCDIRS ${CAER_INCDIRS} ${PROTOBUF_INCLUDE_DIRS})
		MESSAGE(STATUS "PROTOBUF FOUND")
		SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${PROTOBUF_LIBRARIES})
	ENDIF(Protobuf_FOUND)

	#FIND_PACKAGE(BLAS REQUIRED)
	IF(APPLE)
		SET(CAER_INCDIRS ${CAER_INCDIRS} "/System/Library/Frameworks/Accelerate.framework/Versions/A/Frameworks/vecLib.framework/Versions/A/Headers/")
		SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} "-framework Accelerate")
		MESSAGE(STATUS "WARNING !! cblas.h via framework Accelerate path added manually in CMakeList.. please check!")
	ELSE()
		IF(BLAS_FOUND)
			SET(CAER_INCDIRS ${CAER_INCDIRS} ${BLAS_INCLUDE_DIRS})
			MESSAGE(STATUS "BLAS FOUND")
			SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${BLAS_LIBRARIES})
		ENDIF(BLAS_FOUND)
	ENDIF()

        IF(NOT CPU_ONLY)
		FIND_PACKAGE(CUDA)
		SET(CAER_INCDIRS ${CAER_INCDIRS} ${CUDA_INCLUDE_DIRS})
		MESSAGE(STATUS "CUDA FOUND")
		SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} ${CUDA_LIBRARIES})
	ENDIF()
	
	#SET(EXTRA_CAFFE_LIBS "-lglog -lopencv_imgproc -lopencv_highgui -lopencv_core  -lboost_system -lprotobuf -lcaffe")
	SET(EXTRA_CAFFE_LIBS ${EXTRA_CAFFE_LIBS} "-lglog -lboost_system -lprotobuf")
	SET(CAER_CXX_LIBS ${CAER_CXX_LIBS} ${EXTRA_CAFFE_LIBS})

	# We need the C wrapper to use the C++ Caffe library.
	file(GLOB_RECURSE CAER_CAFFEINTERFACE_FILES_CXX modules/caffeinterface/*.cpp)
	SET(CAER_CXX_SRC_FILES ${CAER_CXX_SRC_FILES} ${CAER_CAFFEINTERFACE_FILES_CXX})

	file(GLOB_RECURSE CAER_CAFFEINTERFACE_FILES_C modules/caffeinterface/*.c)
	SET(CAER_SRC_FILES ${CAER_SRC_FILES} ${CAER_CAFFEINTERFACE_FILES_C})
ENDIF()

# Add local directory to include and library paths
SET(CAER_INCDIRS ${CAER_INCDIRS} ${CMAKE_SOURCE_DIR}/)
SET(CAER_LIBDIRS ${CAER_LIBDIRS} ${CMAKE_SOURCE_DIR}/)

INCLUDE_DIRECTORIES(${CAER_INCDIRS})
LINK_DIRECTORIES(${CAER_LIBDIRS})

# Set full RPATH
SET(CMAKE_INSTALL_RPATH ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_BINDIR})
SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
IF (APPLE)
	SET(CMAKE_MACOSX_RPATH TRUE)
ENDIF()

IF (UNIX AND NOT APPLE)
	# Add --as-needed to linker flags for executables.
	SET(CMAKE_EXE_LINKER_FLAGS "-Wl,--as-needed")
ENDIF()

# Compile cAER
file(GLOB_RECURSE CAER_EXT_FILES ext/*.c)
file(GLOB_RECURSE CAER_BASE_FILES base/*.c)

# Add math support
SET(CAER_LIBS ${CAER_LIBS} m)

# Add realtime support, not needed on MacOS X though
IF (NOT APPLE)
	SET(CAER_LIBS ${CAER_LIBS} rt)
ENDIF()

ADD_EXECUTABLE(caer-bin ${CAER_EXT_FILES} ${CAER_BASE_FILES} ${CAER_SRC_FILES} ${CAER_CXX_SRC_FILES} main.c)
TARGET_LINK_LIBRARIES(caer-bin ${CMAKE_THREAD_LIBS_INIT} ${CAER_LIBS} ${CAER_CXX_LIBS})
INSTALL(TARGETS caer-bin DESTINATION ${CMAKE_INSTALL_BINDIR})

# Compile cAERCtl
ADD_EXECUTABLE(caer-ctl ext/sshs/sshs_helper.c utils/ext/linenoise/linenoise.c utils/caerctl/caerctl.c)
INSTALL(TARGETS caer-ctl DESTINATION ${CMAKE_INSTALL_BINDIR})

# Compile UDP stream statistics program
ADD_EXECUTABLE(udpststat utils/udpststat/udpststat.c)
TARGET_LINK_LIBRARIES(udpststat ${LIBCAER_LIBRARIES})
INSTALL(TARGETS udpststat DESTINATION ${CMAKE_INSTALL_BINDIR})

# Compile TCP stream statistics program
ADD_EXECUTABLE(tcpststat utils/tcpststat/tcpststat.c)
TARGET_LINK_LIBRARIES(tcpststat ${LIBCAER_LIBRARIES})
INSTALL(TARGETS tcpststat DESTINATION ${CMAKE_INSTALL_BINDIR})

# Compile Unix local stream statistics program
ADD_EXECUTABLE(unixststat utils/unixststat/unixststat.c)
TARGET_LINK_LIBRARIES(unixststat ${LIBCAER_LIBRARIES})
INSTALL(TARGETS unixststat DESTINATION ${CMAKE_INSTALL_BINDIR})
